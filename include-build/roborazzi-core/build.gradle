plugins {
  id "org.jetbrains.kotlin.multiplatform"
  id "com.android.library"
}
if (System.getenv("INTEGRATION_TEST") != "true") {
  pluginManager.apply("com.vanniktech.maven.publish")
}

java {
  toolchain {
    languageVersion.set(JavaLanguageVersion.of(11))
  }
}

kotlin {
  targetHierarchy.custom {
    it.common {
      it.group("commonJvm") {
        it.withAndroid()
        it.withJvm()
      }
    }
  }

  jvm()
  android {
    publishLibraryVariants("release")
  }

  sourceSets {
    commonMain {
    }
    commonJvmMain {
      dependencies {
        compileOnly libs.gson
        api libs.dropbox.differ
        implementation libs.junit
      }
    }
    commonJvmTest {
      dependencies {
        implementation libs.kotlin.test
        implementation libs.kotlin.test.junit
        implementation libs.junit
      }
    }
    jvmMain {
      dependencies {
        implementation libs.gson
      }
    }
    jvmTest {
      dependencies {
      }
    }

    androidMain {
      dependencies {
        compileOnly libs.robolectric
        compileOnly libs.androidx.compose.ui.test
        compileOnly libs.androidx.compose.ui.test.junit4
        api libs.androidx.test.espresso.core
        implementation libs.androidx.core.ktx
        implementation libs.gson
      }
    }

  }
  sourceSets.all {
    it.languageSettings {
      progressiveMode = true
      optIn("com.github.takahirom.roborazzi.InternalRoborazziApi")
      optIn("com.github.takahirom.roborazzi.ExperimentalRoborazziApi")
    }
  }
}

android {
  namespace 'com.github.takahirom.roborazzi.core'
  compileSdk 33

  defaultConfig {
    minSdk 21
    targetSdk 32

    testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
  }
  compileOptions {
    sourceCompatibility JavaVersion.VERSION_11
    targetCompatibility JavaVersion.VERSION_11
  }
  buildFeatures {
  }
  testOptions {
    unitTests {
      includeAndroidResources = true
    }
  }
}


dependencies {
  compileOnly gradleApi()
}

sourceSets {
  main.java.srcDir 'src/generated/kotlin'
}

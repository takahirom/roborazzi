plugins {
  id "org.jetbrains.kotlin.multiplatform"
  id "com.android.kotlin.multiplatform.library"
  id("org.jetbrains.kotlin.plugin.serialization") version libs.versions.kotlin
}
if (System.getenv("INTEGRATION_TEST") != "true") {
  pluginManager.apply("com.vanniktech.maven.publish")
}

kotlin {
  targetHierarchy.custom {
    it.common {
      it.group("commonJvm") {
        it.withAndroidTarget()
        it.withJvm()
      }
      it.group("ios") {
        it.withIosX64()
        it.withIosArm64()
        it.withIosSimulatorArm64()
      }
    }
  }

  iosX64()
  iosArm64()
  iosSimulatorArm64()

  jvm()

  // AGP 9.0: Use androidLibrary block instead of android block + androidTarget
  androidLibrary {
    namespace = 'com.github.takahirom.roborazzi.core'
    compileSdk = libs.versions.compileSdk.get().toInteger()
    minSdk = libs.versions.minSdk.get().toInteger()

    withHostTest {
    }
  }

  sourceSets {
    commonMain {
      dependencies {
        compileOnly libs.kotlinx.serialization.json
        api libs.dropbox.differ
        implementation libs.kotlinx.io.core
      }
    }
    iosX64 {
      dependsOn(commonMain)
    }
    iosArm64 {
      dependsOn(commonMain)
    }
    iosSimulatorArm64 {
      dependsOn(commonMain)
    }
    commonJvmMain {
      dependencies {
        implementation libs.junit
        // The library is a little bit heavy, so we use compileOnly here.
        // Users need to add this library to their dependencies.
        compileOnly(libs.webp.imageio)
      }
    }
    commonJvmTest {
      dependencies {
        implementation libs.kotlin.test
        implementation libs.kotlin.test.junit
        implementation libs.junit
      }
    }
    jvmMain {
      dependencies {
        implementation libs.kotlinx.serialization.json
      }
    }
    jvmTest {
      dependencies {}
    }

    androidMain {
      // Explicitly depend on commonJvmMain for KMP Android library plugin
      dependsOn(commonJvmMain)
      dependencies {
        compileOnly libs.robolectric
        compileOnly libs.androidx.compose.ui.test
        compileOnly libs.androidx.compose.ui.test.junit4
        api libs.androidx.test.espresso.core
        implementation libs.androidx.core.ktx
        implementation libs.kotlinx.serialization.json
      }
    }
  }
  sourceSets.all {
    it.languageSettings {
      progressiveMode = true
      optIn("com.github.takahirom.roborazzi.InternalRoborazziApi")
      optIn("com.github.takahirom.roborazzi.ExperimentalRoborazziApi")
    }
  }
}

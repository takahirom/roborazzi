plugins {
  id "org.jetbrains.kotlin.multiplatform"
  id "com.android.kotlin.multiplatform.library"
  id("org.jetbrains.kotlin.plugin.serialization") version libs.versions.kotlin
}
if (System.getenv("INTEGRATION_TEST") != "true") {
  pluginManager.apply("com.vanniktech.maven.publish")
}

kotlin {
  iosX64()
  iosArm64()
  iosSimulatorArm64()

  jvm()
  androidLibrary {
    namespace = 'com.github.takahirom.roborazzi.core'
    compileSdk = libs.versions.compileSdk.get().toInteger()
    minSdk = libs.versions.minSdk.get().toInteger()
    withHostTest {
      includeAndroidResources = true
    }
  }

  // Kotlin 2.x: Use sourceSets hierarchy instead of deprecated targetHierarchy.custom
  sourceSets {
    commonMain {
      dependencies {
        compileOnly libs.kotlinx.serialization.json
        api libs.dropbox.differ
        implementation libs.kotlinx.io.core
      }
    }
    // Custom intermediate source set for JVM-based targets (JVM + Android)
    commonJvmMain {
      dependsOn(commonMain)
      dependencies {
        implementation libs.junit
        // The library is a little bit heavy, so we use compileOnly here.
        // Users need to add this library to their dependencies.
        compileOnly(libs.webp.imageio)
      }
    }
    commonJvmTest {
      dependsOn(commonTest)
      dependencies {
        implementation libs.kotlin.test
        implementation libs.kotlin.test.junit
        implementation libs.junit
      }
    }
    jvmMain {
      dependsOn(commonJvmMain)
      dependencies {
        implementation libs.kotlinx.serialization.json
      }
    }
    jvmTest {
      dependsOn(commonJvmTest)
    }
    androidMain {
      dependsOn(commonJvmMain)
      dependencies {
        compileOnly libs.robolectric
        compileOnly libs.androidx.compose.ui.test
        compileOnly libs.androidx.compose.ui.test.junit4
        api libs.androidx.test.espresso.core
        implementation libs.androidx.core.ktx
        implementation libs.kotlinx.serialization.json
      }
    }
    // iOS source sets hierarchy
    iosMain {
      dependsOn(commonMain)
    }
    iosX64Main {
      dependsOn(iosMain)
    }
    iosArm64Main {
      dependsOn(iosMain)
    }
    iosSimulatorArm64Main {
      dependsOn(iosMain)
    }
  }
  sourceSets.all {
    it.languageSettings {
      progressiveMode = true
      optIn("com.github.takahirom.roborazzi.InternalRoborazziApi")
      optIn("com.github.takahirom.roborazzi.ExperimentalRoborazziApi")
    }
  }
}

sourceSets {
  main.java.srcDir 'src/generated/kotlin'
}

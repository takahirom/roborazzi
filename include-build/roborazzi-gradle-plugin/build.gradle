plugins {
  id "org.jetbrains.kotlin.jvm"
  id 'jvm-test-suite'
}
if (System.getenv("INTEGRATION_TEST") != "true") {
  pluginManager.apply("com.vanniktech.maven.publish")
}

apply plugin: 'java-gradle-plugin'

java {
  toolchain {
    languageVersion.set(JavaLanguageVersion.of(11))
  }
}

def integrationTestDep = sourceSets.create('integrationTestDep')

dependencies {
  compileOnly gradleApi()
  compileOnly "com.android.tools.build:gradle:7.3.1"
  compileOnly "org.jetbrains.kotlin:kotlin-gradle-plugin:" + libs.versions.kotlin.get()
  implementation project(':roborazzi-core')
  implementation 'org.json:json:20230227'
  integrationTestDepImplementation libs.android.tools.build.gradle
  integrationTestDepImplementation "org.jetbrains.kotlin:kotlin-gradle-plugin:" + libs.versions.kotlin.get()
  integrationTestDepImplementation "org.jetbrains.compose:compose-gradle-plugin:1.4.3"
}

sourceSets {
  main.java.srcDir 'src/generated/kotlin'
}

test {
  testLogging {
    events "passed", "skipped", "failed", "standardOut", "standardError"
    showStandardStreams = true
    showStackTraces = true
    exceptionFormat = 'full'
  }
}


testing.suites {
  integrationTest(JvmTestSuite) {
    useJUnit()
    sources.kotlin.srcDirs = ["src/integrationTest/java"]
    sources.java.srcDirs = ["src/integrationTest/java"]
    dependencies {
//      implementation project()
      implementation gradleTestKit()
      implementation project(':roborazzi-core')
      implementation "junit:junit:4.13.2"
      implementation libs.android.tools.build.gradle
      implementation 'org.jetbrains.kotlin:kotlin-gradle-plugin:' + libs.versions.kotlin.get()
      implementation "org.jetbrains.compose:compose-gradle-plugin:1.4.3"
    }

    targets {
      all {
        testTask.configure {
          shouldRunAfter(test)
          testLogging {
            events "failed", "standardError"
            showStandardStreams = true
            showStackTraces = true
            exceptionFormat = 'full'
          }
        }
      }
    }
    tasks.withType(PluginUnderTestMetadata.class).named("pluginUnderTestMetadata").configure {
      it.pluginClasspath.from(integrationTestDep.runtimeClasspath)
    }
  }
}

gradlePlugin {
  testSourceSets sourceSets.integrationTest
}

gradlePlugin {
  plugins {
    roborazzi {
      id = 'io.github.takahirom.roborazzi'
      implementationClass = 'io.github.takahirom.roborazzi.RoborazziPlugin'
    }
  }
}


tasks.named('check') {
  dependsOn(testing.suites.integrationTest)
}


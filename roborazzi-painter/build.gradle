plugins {
  id "org.jetbrains.kotlin.multiplatform"
  id("org.jetbrains.kotlin.plugin.serialization") version libs.versions.kotlin
}
if (System.getenv("INTEGRATION_TEST") != "true") {
  pluginManager.apply("com.vanniktech.maven.publish")
}

kotlin {
  iosX64()
  iosArm64()
  iosSimulatorArm64()

  jvm()

  // Kotlin 2.x: Use sourceSets hierarchy instead of deprecated targetHierarchy.custom
  sourceSets {
    commonMain {
      dependencies {
        compileOnly libs.kotlinx.serialization.json
        // Please see settings.gradle
        api "io.github.takahirom.roborazzi:roborazzi-core:$VERSION_NAME"
        api libs.dropbox.differ
      }
    }
    // Custom intermediate source set for JVM-based targets
    commonJvmMain {
      dependsOn(commonMain)
      dependencies {
        compileOnly libs.robolectric.android.all
        implementation libs.kotlinx.io.core
      }
    }
    jvmMain {
      dependsOn(commonJvmMain)
    }
    // iOS source sets hierarchy
    iosMain {
      dependsOn(commonMain)
    }
    iosX64Main {
      dependsOn(iosMain)
    }
    iosArm64Main {
      dependsOn(iosMain)
    }
    iosSimulatorArm64Main {
      dependsOn(iosMain)
    }
  }
}